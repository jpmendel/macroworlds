;;; TRON ;;;

carefully [remove "t1] []
carefully [newturtle "p1] []
carefully [newturtle "p2] []
carefully [newtext "title] []
carefully [newtext "info] []

setbg 1

make "gamestate "start

to startpage
cg
p1, ht
p2, ht

title,
setpos [0 50]
setfontsize 60
setc 130
ct print "TRON
showtext

info,
setpos [0 -50]
setfontsize 20
setc 130
ct print [Press Space to Start]
showtext
end

to ready
p1,
setc 2
setpensize 3
setpos [-100 0]
seth 0
st

p2,
setc 150
setpensize 3
setpos [100 0]
seth 0
st
clean

info, hidetext
title,
setpos [0 0]
ct print "3
wait 1000
ct print "2
wait 1000
ct print "1
wait 1000
hidetext
end

to boundary
op (or xcor < -300 xcor > 300 ycor < -200 ycor > 200)
end

to move
if key? [
 make "key readchar

 p1,
 if and :key = "w not heading = 180 [seth 0]
 if and :key = "s not heading = 0 [seth 180]
 if and :key = "a not heading = 90 [seth 270]
 if and :key = "d not heading = 270 [seth 90]

 p2,
 if and :key = "i not heading = 180 [seth 0]
 if and :key = "k not heading = 0 [seth 180]
 if and :key = "j not heading = 90 [seth 270]
 if and :key = "l not heading = 270 [seth 90]
]

p1,
fd 5
if or boundary not colorunder = 1 [
 win "p2
 make "gamestate "win
]

p2,
fd 5
if or boundary not colorunder = 1 [
 win "p1
 make "gamestate "win
]
end

to win :player
title,
setpos [0 50]
ct
ifelse :player = "p1 [
 print [Red Wins!]
] [
 print [Blue Wins!]
]
showtext

info,
setpos [0 -50]
ct print [Press Space to Play Again]
showtext
end

to game
if :gamestate = "start [
 make "key readchar
 if :key = "space [
  ready
  make "gamestate "play
 ]
]

if :gamestate = "play [move]

if :gamestate = "win [
 make "key readchar
 if :key = "space [
  ready
  make "gamestate "play
 ]
]

wait 30
end

startpage
forever [game]
